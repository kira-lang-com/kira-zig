const std = @import("std");
const builtin = @import("builtin");
const bytecode = @import("kira_bytecode");
const build = @import("kira_build");
const build_def = @import("kira_build_definition");
const diag_messages = @import("kira_diagnostic_messages");
const diagnostics = @import("kira_diagnostics");
const hybrid_runtime = @import("kira_hybrid_runtime");
const kira_live = @import("kira_live");
const manifest_config = @import("kira_manifest");
const package_manager = @import("kira_package_manager");
const runtime_abi = @import("kira_runtime_abi");
const vm_runtime = @import("kira_vm_runtime");
const support = @import("../support.zig");

extern "c" fn setenv(name: [*:0]const u8, value: [*:0]const u8, overwrite: c_int) c_int;
extern "c" fn unsetenv(name: [*:0]const u8) c_int;
extern "kernel32" fn SetEnvironmentVariableA(name: [*:0]const u8, value: ?[*:0]const u8) callconv(.winapi) c_int;

pub fn execute(allocator: std.mem.Allocator, args: []const []const u8, stdout: anytype, stderr: anytype) !void {
    const parsed = try parseArgs(args);
    if (parsed.runner != null and parsed.runner.? == .web) {
        return executeWebRunner(allocator, parsed, stdout, stderr);
    }
    build.setTimingsEnabled(parsed.timings or timingsEnvEnabled());
    build.setNativePreparationMode(.artifacts_only);
    defer build.setNativePreparationMode(.full);
    const previous_trace = runtime_abi.executionTraceEnabled();
    runtime_abi.setExecutionTraceEnabled(parsed.trace_execution);
    defer runtime_abi.setExecutionTraceEnabled(previous_trace);
    const input = support.resolveCliInputWithDiagnostics(allocator, parsed.input_path, stderr) catch |err| switch (err) {
        error.InvalidProjectPath => {
            try support.renderStandaloneDiagnostic(stderr, try diag_messages.CliMessages.invalidProjectPath(allocator, parsed.input_path));
            return error.CommandFailed;
        },
        error.ProjectManifestNotFound => {
            try support.renderStandaloneDiagnostic(stderr, try diag_messages.PackageMessages.missingProjectManifest(allocator, parsed.input_path));
            return error.CommandFailed;
        },
        else => return err,
    };
    try support.validateTargetSelection(allocator, stderr, .run, input);
    const backend = parsed.backend orelse input.default_backend orelse .vm;
    const source_path = input.target.source_path.?;

    if (input.target.root_path) |project_root| {
        var package_diagnostics = std.array_list.Managed(diagnostics.Diagnostic).init(allocator);
        _ = package_manager.syncProject(allocator, project_root, support.versionString(), .{
            .offline = parsed.offline,
            .locked = parsed.locked,
        }, &package_diagnostics) catch |err| {
            if (err == error.DiagnosticsEmitted) {
                try support.renderStandaloneDiagnostics(stderr, package_diagnostics.items);
                return error.CommandFailed;
            }
            return err;
        };
    }
    try support.warnNativePreparationState(allocator, stderr, "run", input, backend);

    try support.logFrontendStarted(stderr, "run", source_path);
    var system = build.BuildSystem.init(allocator);
    const output_root = try support.outputRoot(allocator, input.target.root_path);
    defer allocator.free(output_root);
    try support.ensurePath(output_root);
    const stem = input.target.project_name orelse std.fs.path.stem(source_path);
    const output_path = try runOutputPath(allocator, output_root, stem, backend);
    const result = try system.build(.{
        .source_path = source_path,
        .output_path = output_path,
        .target = .{ .execution = backend },
    });
    if (result.failed()) {
        try support.logBuildAborted(stderr, "run", result.failure_kind.?, source_path);
        if (result.source) |source| {
            try support.renderDiagnostics(stderr, &source, result.diagnostics);
        }
        return error.CommandFailed;
    }

    switch (backend) {
        .vm => {
            const bytecode_artifact = findBytecode(result.artifacts) orelse return error.MissingBytecodeArtifact;
            const runtime_allocator = std.heap.smp_allocator;
            const module = try bytecode.Module.readFromFile(runtime_allocator, bytecode_artifact.path);
            var vm = vm_runtime.Vm.init(runtime_allocator);
            defer vm.deinit();
            var original_cwd = try std.Io.Dir.cwd().openDir(std.Options.debug_io, ".", .{});
            defer {
                if (input.target.root_path) |_| std.process.setCurrentDir(std.Options.debug_io, original_cwd) catch {};
                original_cwd.close(std.Options.debug_io);
            }
            if (input.target.root_path) |root| {
                var dir = try std.Io.Dir.openDirAbsolute(std.Options.debug_io, root, .{});
                defer dir.close(std.Options.debug_io);
                try std.process.setCurrentDir(std.Options.debug_io, dir);
            }
            const graphics_quit_after = graphicsQuitAfterFrames(parsed.quit_after_ns);
            var env_restore = try ScopedEnv.setInt(allocator, "KIRA_GRAPHICS_QUIT_AFTER_FRAMES", graphics_quit_after);
            defer env_restore.deinit();
            var ffi_dispatcher = vm_runtime.FfiDispatcher.init(runtime_allocator, &module);
            defer ffi_dispatcher.deinit();
            for (result.native_libraries) |library| {
                try ffi_dispatcher.registerLibrary(library.name, library.artifact_path);
            }
            vm.runMainWithHooks(&module, stdout, .{
                .context = &ffi_dispatcher,
                .call_native = vm_runtime.FfiDispatcher.hook,
            }) catch |err| {
                // A failed FFI dispatch records a precise message even when the
                // error is not RuntimeFailure (e.g. an unsupported FFI type).
                if (ffi_dispatcher.lastError()) |ffi_message| {
                    try stderr.print("vm ffi failure: {s}\n", .{ffi_message});
                    return error.CommandFailed;
                }
                if (err == error.RuntimeFailure) {
                    if (vm.lastError()) |message| {
                        try stderr.print("vm runtime failure: {s}\n", .{message});
                        return error.CommandFailed;
                    }
                }
                return err;
            };
            if (runtimeMemoryReportEnabled()) vm.emitMemoryReport("vm");
            if (runtimeMemoryDetailEnabled()) vm.emitMemoryDetail();
        },
        .llvm_native => {
            const executable = findExecutable(result.artifacts) orelse return error.MissingExecutableArtifact;
            try runExecutable(allocator, executable.path, input.target.root_path, parsed.trace_execution, parsed.quit_after_ns, stdout, stderr);
        },
        .wasm32_emscripten => {
            const executable = findExecutable(result.artifacts) orelse return error.MissingExecutableArtifact;
            try runWasmNode(allocator, executable.path, input.target.root_path, stdout, stderr);
        },
        .hybrid => {
            const manifest_artifact = findHybridManifest(result.artifacts) orelse return error.MissingHybridManifestArtifact;
            if (parsed.quit_after_ns) |duration_ns| {
                try runHybridArtifactBounded(allocator, manifest_artifact.path, input.target.root_path, duration_ns, stdout, stderr);
                return;
            }
            const runtime_allocator = std.heap.smp_allocator;
            const manifest = try hybrid_runtime.loadHybridModule(runtime_allocator, manifest_artifact.path);
            var runtime = try hybrid_runtime.HybridRuntime.init(runtime_allocator, manifest);
            var runtime_deinitialized = false;
            defer if (!runtime_deinitialized) runtime.deinit();
            var original_cwd = try std.Io.Dir.cwd().openDir(std.Options.debug_io, ".", .{});
            defer {
                if (input.target.root_path) |_| std.process.setCurrentDir(std.Options.debug_io, original_cwd) catch {};
                original_cwd.close(std.Options.debug_io);
            }
            if (input.target.root_path) |root| {
                var dir = try std.Io.Dir.openDirAbsolute(std.Options.debug_io, root, .{});
                defer dir.close(std.Options.debug_io);
                try std.process.setCurrentDir(std.Options.debug_io, dir);
            }
            const graphics_quit_after = graphicsQuitAfterFrames(parsed.quit_after_ns);
            var env_restore = try ScopedEnv.setInt(allocator, "KIRA_GRAPHICS_QUIT_AFTER_FRAMES", graphics_quit_after);
            defer env_restore.deinit();
            runtime.run() catch |err| {
                if (err == error.RuntimeFailure) {
                    if (runtime.vm.lastError()) |message| {
                        try stderr.print("hybrid runtime failure: {s}\n", .{message});
                        return error.CommandFailed;
                    }
                }
                return err;
            };
            if (runtimeMemoryDetailEnabled()) runtime.vm.emitMemoryDetail();
            if (runtimeMemoryReportEnabled()) {
                runtime.deinit();
                runtime_deinitialized = true;
                runtime.vm.emitMemoryReport("hybrid");
            }
        },
    }
}

pub fn executeHybridArtifact(allocator: std.mem.Allocator, args: []const []const u8, stdout: anytype, stderr: anytype) !void {
    _ = allocator;
    _ = stdout;
    const parsed = try parseHybridArtifactArgs(args);
    const runtime_allocator = std.heap.smp_allocator;
    const manifest = try hybrid_runtime.loadHybridModule(runtime_allocator, parsed.manifest_path);
    var runtime = try hybrid_runtime.HybridRuntime.init(runtime_allocator, manifest);
    defer runtime.deinit();
    var original_cwd = try std.Io.Dir.cwd().openDir(std.Options.debug_io, ".", .{});
    defer {
        if (parsed.cwd) |_| std.process.setCurrentDir(std.Options.debug_io, original_cwd) catch {};
        original_cwd.close(std.Options.debug_io);
    }
    if (parsed.cwd) |cwd| {
        var dir = try std.Io.Dir.openDirAbsolute(std.Options.debug_io, cwd, .{});
        defer dir.close(std.Options.debug_io);
        try std.process.setCurrentDir(std.Options.debug_io, dir);
    }
    runtime.run() catch |err| {
        if (err == error.RuntimeFailure) {
            if (runtime.vm.lastError()) |message| {
                try stderr.print("hybrid runtime failure: {s}\n", .{message});
                return error.CommandFailed;
            }
        }
        return err;
    };
}

const HybridArtifactArgs = struct {
    manifest_path: []const u8,
    cwd: ?[]const u8 = null,
};

fn parseHybridArtifactArgs(args: []const []const u8) !HybridArtifactArgs {
    var manifest_path: ?[]const u8 = null;
    var cwd: ?[]const u8 = null;
    var index: usize = 0;
    while (index < args.len) : (index += 1) {
        const arg = args[index];
        if (std.mem.eql(u8, arg, "--manifest")) {
            index += 1;
            if (index >= args.len) return error.InvalidArguments;
            manifest_path = args[index];
            continue;
        }
        if (std.mem.eql(u8, arg, "--cwd")) {
            index += 1;
            if (index >= args.len) return error.InvalidArguments;
            cwd = args[index];
            continue;
        }
        return error.InvalidArguments;
    }
    return .{ .manifest_path = manifest_path orelse return error.InvalidArguments, .cwd = cwd };
}

const ParsedArgs = struct {
    runner: ?manifest_config.RunnerId = null,
    backend: ?build_def.ExecutionTarget = null,
    surface: manifest_config.WebSurface = .dom,
    offline: bool = false,
    locked: bool = false,
    trace_execution: bool = false,
    timings: bool = false,
    quit_after_ns: ?u64 = null,
    input_path: []const u8,
};

fn parseArgs(args: []const []const u8) !ParsedArgs {
    var runner: ?manifest_config.RunnerId = null;
    var backend: ?build_def.ExecutionTarget = null;
    var surface: manifest_config.WebSurface = .dom;
    var offline = false;
    var locked = false;
    var trace_execution = false;
    var timings = false;
    var quit_after_ns: ?u64 = null;
    var input_path: ?[]const u8 = null;

    var index: usize = 0;
    while (index < args.len) : (index += 1) {
        const arg = args[index];
        if (runner == null and input_path == null and !std.mem.startsWith(u8, arg, "-")) {
            if (manifest_config.RunnerId.parse(arg)) |parsed_runner| {
                if (parsed_runner == .web) {
                    runner = parsed_runner;
                    continue;
                }
            }
        }
        if (std.mem.eql(u8, arg, "--backend")) {
            index += 1;
            if (index >= args.len) return error.InvalidArguments;
            backend = parseBackend(args[index]) orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--offline")) {
            offline = true;
            continue;
        }
        if (std.mem.eql(u8, arg, "--locked")) {
            locked = true;
            continue;
        }
        if (std.mem.eql(u8, arg, "--trace-execution")) {
            trace_execution = true;
            continue;
        }
        if (std.mem.eql(u8, arg, "--timings")) {
            timings = true;
            continue;
        }
        if (std.mem.eql(u8, arg, "--quit-after") or std.mem.eql(u8, arg, "-quit-after")) {
            index += 1;
            if (index >= args.len) return error.InvalidArguments;
            quit_after_ns = parseDurationNs(args[index]) orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--surface")) {
            index += 1;
            if (index >= args.len) return error.InvalidArguments;
            surface = manifest_config.WebSurface.parse(args[index]) orelse return error.InvalidArguments;
            continue;
        }
        if (input_path != null) return error.InvalidArguments;
        input_path = arg;
    }

    return .{
        .runner = runner,
        .backend = backend,
        .surface = surface,
        .offline = offline,
        .locked = locked,
        .trace_execution = trace_execution,
        .timings = timings,
        .quit_after_ns = quit_after_ns,
        .input_path = input_path orelse support.defaultCommandInputPath(),
    };
}

fn executeWebRunner(allocator: std.mem.Allocator, parsed: ParsedArgs, stdout: anytype, stderr: anytype) !void {
    var live_args = std.array_list.Managed([]const u8).init(allocator);
    try live_args.append("web");
    try live_args.append(parsed.input_path);
    try live_args.appendSlice(&.{ "--surface", parsed.surface.label(), "--headless" });
    if (parsed.quit_after_ns) |duration_ns| {
        try live_args.append("--quit-after");
        try live_args.append(try std.fmt.allocPrint(allocator, "{d}ms", .{duration_ns / std.time.ns_per_ms}));
    } else {
        try live_args.appendSlice(&.{ "--quit-after", "1s" });
    }
    try kira_live.execute(allocator, live_args.items, stdout, stderr);
}

fn parseDurationNs(value: []const u8) ?u64 {
    if (std.mem.endsWith(u8, value, "ms")) {
        const number = value[0 .. value.len - 2];
        if (number.len == 0) return null;
        const parsed = std.fmt.parseInt(u64, number, 10) catch return null;
        if (parsed == 0) return null;
        return std.math.mul(u64, parsed, std.time.ns_per_ms) catch return null;
    }
    if (std.mem.endsWith(u8, value, "s")) {
        const number = value[0 .. value.len - 1];
        if (number.len == 0) return null;
        const parsed = std.fmt.parseInt(u64, number, 10) catch return null;
        if (parsed == 0) return null;
        return std.math.mul(u64, parsed, std.time.ns_per_s) catch return null;
    }
    const parsed = std.fmt.parseInt(u64, value, 10) catch return null;
    if (parsed == 0) return null;
    return std.math.mul(u64, parsed, std.time.ns_per_s) catch return null;
}

fn timingsEnvEnabled() bool {
    if (!builtin.link_libc) return false;
    const raw = std.c.getenv("KIRA_TIMINGS") orelse return false;
    const value = std.mem.span(raw);
    return value.len != 0 and !std.mem.eql(u8, value, "0") and !std.mem.eql(u8, value, "false");
}

fn runtimeMemoryReportEnabled() bool {
    if (!builtin.link_libc) return false;
    const raw = std.c.getenv("KIRA_RUNTIME_MEMORY_REPORT") orelse return false;
    const value = std.mem.span(raw);
    return value.len != 0 and !std.mem.eql(u8, value, "0") and !std.mem.eql(u8, value, "false");
}

fn runtimeMemoryDetailEnabled() bool {
    if (!builtin.link_libc) return false;
    const raw = std.c.getenv("KIRA_RUNTIME_MEMORY_DETAIL") orelse return false;
    const value = std.mem.span(raw);
    return value.len != 0 and !std.mem.eql(u8, value, "0") and !std.mem.eql(u8, value, "false");
}

fn parseBackend(arg: []const u8) ?build_def.ExecutionTarget {
    if (std.mem.eql(u8, arg, "vm")) return .vm;
    if (std.mem.eql(u8, arg, "llvm")) return .llvm_native;
    if (std.mem.eql(u8, arg, "wasm") or std.mem.eql(u8, arg, "wasm32-emscripten")) return .wasm32_emscripten;
    if (std.mem.eql(u8, arg, "hybrid")) return .hybrid;
    return null;
}

fn findExecutable(artifacts: []const build_def.Artifact) ?build_def.Artifact {
    for (artifacts) |artifact| {
        if (artifact.kind == .executable) return artifact;
    }
    return null;
}

fn findBytecode(artifacts: []const build_def.Artifact) ?build_def.Artifact {
    for (artifacts) |artifact| {
        if (artifact.kind == .bytecode) return artifact;
    }
    return null;
}

fn findHybridManifest(artifacts: []const build_def.Artifact) ?build_def.Artifact {
    for (artifacts) |artifact| {
        if (artifact.kind == .hybrid_manifest) return artifact;
    }
    return null;
}

fn runOutputPath(allocator: std.mem.Allocator, output_root: []const u8, stem: []const u8, backend: build_def.ExecutionTarget) ![]const u8 {
    return switch (backend) {
        .vm => std.fmt.allocPrint(allocator, "{s}/{s}.run.kbc", .{ output_root, stem }),
        .llvm_native => std.fmt.allocPrint(allocator, "{s}/{s}.run{s}", .{ output_root, stem, build.executableExtension() }),
        .wasm32_emscripten => std.fmt.allocPrint(allocator, "{s}/{s}.run.js", .{ output_root, stem }),
        .hybrid => std.fmt.allocPrint(allocator, "{s}/{s}.run.khm", .{ output_root, stem }),
    };
}

fn runWasmNode(allocator: std.mem.Allocator, path: []const u8, project_root: ?[]const u8, stdout: anytype, stderr: anytype) !void {
    const process_environ = inheritedProcessEnviron();
    var io_impl: std.Io.Threaded = .init(std.heap.smp_allocator, .{ .environ = process_environ });
    defer io_impl.deinit();
    const result = try std.process.run(allocator, io_impl.io(), .{
        .argv = &.{ "node", path },
        .cwd = if (project_root) |root| .{ .path = root } else .inherit,
        .expand_arg0 = .expand,
    });
    defer allocator.free(result.stdout);
    defer allocator.free(result.stderr);
    if (result.stdout.len > 0) try stdout.writeAll(result.stdout);
    if (result.stderr.len > 0) try stderr.writeAll(result.stderr);
    if (result.term == .exited and result.term.exited == 0) return;
    try stderr.print("wasm32-emscripten executable failed: {s}\n", .{path});
    return error.CommandFailed;
}

fn runExecutable(
    allocator: std.mem.Allocator,
    path: []const u8,
    project_root: ?[]const u8,
    trace_execution: bool,
    quit_after_ns: ?u64,
    stdout: anytype,
    stderr: anytype,
) !void {
    if (quit_after_ns) |duration_ns| {
        return runExecutableBounded(allocator, path, project_root, trace_execution, duration_ns, stdout, stderr);
    }
    const process_environ = inheritedProcessEnviron();
    var io_impl: std.Io.Threaded = .init(std.heap.smp_allocator, .{ .environ = process_environ });
    defer io_impl.deinit();
    var environ_map = if (trace_execution) try std.process.Environ.createMap(process_environ, allocator) else null;
    defer if (environ_map) |*map| map.deinit();
    if (environ_map) |*map| {
        try map.put("KIRA_TRACE_EXECUTION", "1");
    }
    const result = try std.process.run(allocator, io_impl.io(), .{
        .argv = &.{path},
        .cwd = if (project_root) |root| .{ .path = root } else .inherit,
        .environ_map = if (environ_map) |*map| map else null,
    });
    defer allocator.free(result.stdout);
    defer allocator.free(result.stderr);

    if (result.stdout.len > 0) try stdout.writeAll(result.stdout);
    if (result.term == .exited and result.term.exited == 0) {
        if (result.stderr.len > 0) try stderr.writeAll(result.stderr);
        return;
    }

    try stderr.print("native executable failed: {s}\n", .{path});
    switch (result.term) {
        .exited => |code| try stderr.print("  exit code: {d}\n", .{code}),
        .signal => |signal| try stderr.print("  signal: {d}\n", .{signal}),
        .stopped => |signal| try stderr.print("  stopped by signal: {d}\n", .{signal}),
        .unknown => |code| try stderr.print("  status: {d}\n", .{code}),
    }
    try writeNativeFailureGuidance(result.term, stderr);
    if (result.stderr.len > 0) {
        try stderr.writeAll("  stderr:\n");
        try stderr.writeAll(result.stderr);
        if (result.stderr[result.stderr.len - 1] != '\n') try stderr.writeAll("\n");
    } else {
        try stderr.writeAll("  stderr: <empty>\n");
    }
    if (result.stdout.len > 0) {
        try stderr.writeAll("  stdout:\n");
        try stderr.writeAll(result.stdout);
        if (result.stdout[result.stdout.len - 1] != '\n') try stderr.writeAll("\n");
    }
    if (project_root) |cwd| {
        try stderr.print("  cwd: {s}\n", .{cwd});
    }
    try stderr.print("  command: {s}\n", .{path});
    if (builtin.os.tag == .windows and result.term == .exited and result.term.exited == 9) {
        try stderr.writeAll("  note: Windows may report native fail-fast statuses through the low exit byte; running the executable directly can reveal the full NTSTATUS.\n");
    }
    return error.NativeRunFailed;
}

fn writeNativeFailureGuidance(term: std.process.Child.Term, stderr: anytype) !void {
    const signal = switch (term) {
        .signal => |value| value,
        .stopped => |value| value,
        else => null,
    };
    if (signal) |value| {
        const signal_number = @intFromEnum(value);
        if (signal_number == 11) {
            try stderr.writeAll("  diagnostic: native process terminated with signal 11 (segmentation fault).\n");
            try stderr.writeAll("  note: this usually means native code dereferenced invalid Kira-owned storage, such as an aggregate, array, string, native state, or FFI callback pointer.\n");
        } else {
            try stderr.print("  diagnostic: native process terminated by signal {d} before returning a Kira diagnostic.\n", .{signal_number});
        }
        try stderr.writeAll("  next: compare `kira run --backend vm`, `kira run --backend hybrid`, and `kira run --backend llvm`; if only native or hybrid fails, inspect aggregate defaults, array/string fields, and native bridge ownership.\n");
    }
}

fn runExecutableBounded(
    allocator: std.mem.Allocator,
    path: []const u8,
    project_root: ?[]const u8,
    trace_execution: bool,
    quit_after_ns: u64,
    stdout: anytype,
    stderr: anytype,
) !void {
    _ = stdout;
    const process_environ = inheritedProcessEnviron();
    var io_impl: std.Io.Threaded = .init(std.heap.smp_allocator, .{ .environ = process_environ });
    defer io_impl.deinit();
    var environ_map = try std.process.Environ.createMap(process_environ, allocator);
    defer environ_map.deinit();
    if (trace_execution) try environ_map.put("KIRA_TRACE_EXECUTION", "1");
    if (graphicsQuitAfterFrames(quit_after_ns)) |frames| {
        const value = try std.fmt.allocPrint(allocator, "{d}", .{frames});
        try environ_map.put("KIRA_GRAPHICS_QUIT_AFTER_FRAMES", value);
    }

    const io = io_impl.io();
    var child = try std.process.spawn(io, .{
        .argv = &.{path},
        .cwd = if (project_root) |root| .{ .path = root } else .inherit,
        .environ_map = &environ_map,
        .stdin = .ignore,
        .stdout = .inherit,
        .stderr = .inherit,
    });
    return waitBoundedChild(io, &child, quit_after_ns, "native executable", path, project_root, stderr);
}

/// Wait for a bounded (quit-after) child up to the deadline, then decide success
/// vs. failure based on how it ended — instead of blind-sleeping and reporting
/// success unconditionally (which masked first-frame crashes, a Core Law #2 false
/// success surface). A child still alive at the deadline is the legitimate
/// quit-after case: we kill it and report success. A clean self-exit (code 0,
/// e.g. KIRA_GRAPHICS_QUIT_AFTER_FRAMES reached) is also success. Any abnormal
/// early exit (non-zero code, signal, stopped) is surfaced and propagated as a
/// failure.
fn waitBoundedChild(
    io: std.Io,
    child: *std.process.Child,
    quit_after_ns: u64,
    label: []const u8,
    path: []const u8,
    project_root: ?[]const u8,
    stderr: anytype,
) !void {
    const grace_ns = 5 * std.time.ns_per_s;
    if (try kira_live.waitChildTermBefore(child, quit_after_ns + grace_ns)) |term| {
        if (term == .exited and term.exited == 0) {
            try stderr.print("{s} quit-after elapsed: {s}\n", .{ label, path });
            return;
        }
        try stderr.print("{s} crashed before quit-after: {s}\n", .{ label, path });
        switch (term) {
            .exited => |code| try stderr.print("  exit code: {d}\n", .{code}),
            .signal => |sig| try stderr.print("  signal: {d}\n", .{@intFromEnum(sig)}),
            .stopped => |sig| try stderr.print("  stopped by signal: {d}\n", .{@intFromEnum(sig)}),
            .unknown => |code| try stderr.print("  status: {d}\n", .{code}),
        }
        try writeNativeFailureGuidance(term, stderr);
        if (project_root) |cwd| try stderr.print("  cwd: {s}\n", .{cwd});
        return error.NativeRunFailed;
    }
    // Still running at the deadline: legitimate quit-after. Kill and report success.
    child.kill(io);
    try stderr.print("{s} quit-after elapsed: {s}\n", .{ label, path });
}

fn runHybridArtifactBounded(
    allocator: std.mem.Allocator,
    manifest_path: []const u8,
    project_root: ?[]const u8,
    quit_after_ns: u64,
    stdout: anytype,
    stderr: anytype,
) !void {
    _ = stdout;
    const self_exe = try resolveKiracExecutable(allocator);
    defer allocator.free(self_exe);
    var argv = std.array_list.Managed([]const u8).init(allocator);
    try argv.appendSlice(&.{ self_exe, "__run-hybrid-artifact", "--manifest", manifest_path });
    if (project_root) |root| try argv.appendSlice(&.{ "--cwd", root });

    const process_environ = inheritedProcessEnviron();
    var io_impl: std.Io.Threaded = .init(std.heap.smp_allocator, .{ .environ = process_environ });
    defer io_impl.deinit();
    var environ_map = try std.process.Environ.createMap(process_environ, allocator);
    defer environ_map.deinit();
    if (graphicsQuitAfterFrames(quit_after_ns)) |frames| {
        const value = try std.fmt.allocPrint(allocator, "{d}", .{frames});
        try environ_map.put("KIRA_GRAPHICS_QUIT_AFTER_FRAMES", value);
    }
    const io = io_impl.io();
    var child = try std.process.spawn(io, .{
        .argv = argv.items,
        .cwd = if (project_root) |root| .{ .path = root } else .inherit,
        .environ_map = &environ_map,
        .stdin = .ignore,
        .stdout = .inherit,
        .stderr = .inherit,
    });
    return waitBoundedChild(io, &child, quit_after_ns, "hybrid runtime", manifest_path, project_root, stderr);
}

fn resolveKiracExecutable(allocator: std.mem.Allocator) ![]const u8 {
    const toolchain_root = try support.resolveManagedToolchainRoot(allocator);
    defer allocator.free(toolchain_root);
    return std.fs.path.join(allocator, &.{ toolchain_root, "bin", support.primaryExecutableName() });
}

fn graphicsQuitAfterFrames(quit_after_ns: ?u64) ?u64 {
    const duration_ns = quit_after_ns orelse return null;
    const frame_ns = std.time.ns_per_s / 60;
    return @max(1, (duration_ns + frame_ns - 1) / frame_ns);
}

const ScopedEnv = struct {
    allocator: std.mem.Allocator,
    name_z: ?[:0]u8 = null,
    active: bool = false,

    fn setInt(allocator: std.mem.Allocator, name: []const u8, value: ?u64) !ScopedEnv {
        const int_value = value orelse return .{ .allocator = allocator };
        if (!builtin.link_libc) return .{ .allocator = allocator };
        const name_z = try allocator.dupeZ(u8, name);
        const text_value = try std.fmt.allocPrint(allocator, "{d}", .{int_value});
        defer allocator.free(text_value);
        const value_z = try allocator.dupeZ(u8, text_value);
        defer allocator.free(value_z);
        if (builtin.os.tag == .windows) {
            if (SetEnvironmentVariableA(name_z.ptr, value_z.ptr) == 0) return error.EnvironmentUpdateFailed;
        } else if (setenv(name_z.ptr, value_z.ptr, 1) != 0) {
            return error.EnvironmentUpdateFailed;
        }
        return .{ .allocator = allocator, .name_z = name_z, .active = true };
    }

    fn deinit(self: *ScopedEnv) void {
        if (!self.active) return;
        if (self.name_z) |name_z| {
            if (builtin.os.tag == .windows) {
                _ = SetEnvironmentVariableA(name_z.ptr, null);
            } else {
                _ = unsetenv(name_z.ptr);
            }
            self.allocator.free(name_z);
        }
    }
};

fn inheritedProcessEnviron() std.process.Environ {
    return switch (builtin.os.tag) {
        .windows => .{ .block = .global },
        .wasi, .emscripten, .freestanding, .other => .empty,
        else => .{ .block = .{ .slice = currentPosixEnvironBlock() } },
    };
}

fn currentPosixEnvironBlock() [:null]const ?[*:0]const u8 {
    if (!builtin.link_libc) return &.{};

    const environ = std.c.environ;
    var len: usize = 0;
    while (environ[len] != null) : (len += 1) {}
    return environ[0..len :null];
}

test "parseArgs recognizes trace execution" {
    const parsed = try parseArgs(&.{ "--trace-execution", "--backend", "hybrid", "examples/hello.kira" });
    try std.testing.expect(parsed.trace_execution);
    try std.testing.expectEqual(build_def.ExecutionTarget.hybrid, parsed.backend.?);
    try std.testing.expectEqualStrings("examples/hello.kira", parsed.input_path);
}
